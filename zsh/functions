function code {
  cd ~/Code/$1
}

_code() {
  compadd `ls ~/Code`
}

# Completion for code() function
compdef _code code

function g {
  if [[ $# > 0 ]]
  then
    git $@
  else
    git status
  fi
}

# Complete g like git
compdef g=git

function commands {
  history | awk '{a[$2]++}END{for(i in a){print a[i] " " i}}' | sort -rn | head
}

function ta() {
  tmux new-session -c ~/Code/$1 -As $1
}

function gwtc() {
  local branch_name="$1"
  local current_branch=$(git branch --show-current 2>/dev/null)
  local base_branch="${2:-$current_branch}"
  local current_folder=$(basename "$PWD")
  local target_dir="../${current_folder}.${branch_name}"

  if [[ -z "$base_branch" ]]; then
    base_branch="master"
  fi

  if [[ -z "$branch_name" ]]; then
    echo "Usage: gwtc <new-branch-name> [base-branch]"
    return 1
  fi

  # Create worktree: ../current.branch
  git worktree add -b "$branch_name" "$target_dir" "$base_branch" || return 1

  # Navigate and open Claude
  cd "$target_dir" && cc
}

function gwtr() {
  local current_worktree=$(git rev-parse --show-toplevel 2>/dev/null)
  local git_dir=$(git rev-parse --git-dir 2>/dev/null)
  local common_dir=$(git rev-parse --git-common-dir 2>/dev/null)
  local main_worktree=$(git worktree list --porcelain 2>/dev/null | awk '/^worktree /{print substr($0,10); exit}')

  if [[ -z "$current_worktree" ]]; then
    echo "Not inside a git repository."
    return 1
  fi

  if [[ "$git_dir" == "$common_dir" ]]; then
    echo "You're in the main worktree. Refusing to remove it."
    return 1
  fi

  if [[ -z "$main_worktree" ]]; then
    echo "Could not determine main worktree."
    return 1
  fi

  cd "$main_worktree" || return 1
  git worktree remove "$current_worktree"
}
